#!/usr/bin/perl
###############################################################################
#
#    Zevenet Software License
#    This file is part of the Zevenet Load Balancer software package.
#
#    Copyright (C) 2014-today ZEVENET SL, Sevilla (Spain)
#
#    This program is free software: you can redistribute it and/or modify
#    it under the terms of the GNU Affero General Public License as
#    published by the Free Software Foundation, either version 3 of the
#    License, or any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU Affero General Public License for more details.
#
#    You should have received a copy of the GNU Affero General Public License
#    along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
###############################################################################

use strict;
use warnings;

use Time::Local;
use Zevenet::Log;
use Zevenet::Debug;
use Zevenet::Config;
use Data::Dumper;

sub include;

my $DEBUG    = &debug();
my $SYMKEY   = "1135628147310";
my $mod;
my $program_name =
    ( $0 ne '-e' ) ? $0
  : ( exists $ENV{ SCRIPT_NAME } && $ENV{ SCRIPT_NAME } =~ /\/zapi.cgi$/ ) ? $ENV{ SCRIPT_NAME }
  : ( exists $ENV{ _ } && $ENV{ _ } !~ /enterprise.bin$/ ) ? $ENV{ _ }
  :                                                          $^X;
my $basename = ( split ( '/', $program_name ) )[-1];

&zenlog("[ENTERPRISE.BIN] \$0 = '$0'");
&zenlog("[ENTERPRISE.BIN] \$^X = '$^X'");
&zenlog("[ENTERPRISE.BIN] \$ENV\{_\} = '$ENV{_}'") if exists $ENV{_};
&zenlog("[ENTERPRISE.BIN] \$program_name = '$program_name'");
&zenlog("[ENTERPRISE.BIN] \$basename = '$basename'");
&zenlog("[ENTERPRISE.BIN] \@ARGV = '@ARGV'");
#~ &zenlog("[ENTERPRISE.BIN] \%ENV = " . Dumper \%ENV );

# Run encrypted command
if ( $basename ne 'enterprise.bin' && $basename ne 'zevenet' )
{
	$basename =~ s!\.(?:pl|cgi)$!!; # Remove trailing .pl or .cgi if exists

	if ( $basename eq 'zapi' )
	{
		my $api_ver = ( split ( '/', $program_name ) )[-2]; # get zapi.cgi directory name
		$basename = "$basename-$api_ver"; # add version. E.g.: 'zapi-v3'
	}

	$mod->{ name } = "Zevenet::Cmd::$basename";

#	if ( $DEBUG )
#	{
#		my $msg = '';
#
#		$msg = "enterprise.bin called: $0";
#		#~ &zenlog( $msg );
#		#~ say( $msg );
#
#		$msg = "enterprise.bin basename: $basename";
#		#~ say( $msg );
#
#		$msg = "Module: $mod";
#		#~ &zenlog( $msg );
#		#~ say( $msg );
#	}

	include $mod->{ name };
	exit;
}

require Zevenet::Validate;
require Zevenet::SystemInfo;

$mod->{ name } = shift @ARGV;

# check module name provided
unless ( $mod->{ name } )
{
	print STDERR "enterprise.bin: missing operand
Try 'enterprise.bin --help' for more information.\n";

	exit 1;
}

# command help and usage instructions
if ( $mod->{ name } =~ /^(?:-h|-help|--help)$/ )
{
	print "Usage: enterprise.bin MODULE [FUNCTION]
Decrypt Zevenet Enterprise Edition Module.

Example:\tenterprise.bin Zevenet::API31::System::Cluster get_cluster < JSONFILE

Run encrypted module accepting arguments from STDIN in JSON format, returning JSON output.
";

	exit 0;
}

&getModuleInfo( $mod );


#########################
## Required conditions ##
#########################

# check function name provided
my $func = shift @ARGV;
unless ( $func )
{
	die "$basename: No function name received.";
}


############################
## Activation certificate ##
############################

# check if the activation certificate is valid
my $hostname = &getHostname();
my $basedir  = &getGlobalConfiguration('basedir');

my $skip_cert_check = (    $mod->{ name } =~ /Zevenet::API3\d::Certificate::Activation/
						&& $func   eq 'upload_activation_certificate' );

unless ( $skip_cert_check )
{
	my $swcert = &certcontrol();

	if ( $swcert > 0 )
	{
		die "$basename: Invalid activation certificate ($swcert)";
	}
}



## Module. Find, decrypt and load the module
include $mod->{ name }, $func;


# Arguments. Passed from STDIN as a JSON string
# Load Input, then Decode JSON from intput string

# get input for api function
my $input;
{
	local $/ = undef;
	$input = <STDIN>;
}

# my $input_l = length $input;
# unless ( $input_l >= 2 )
# { &zenlog("Function args input length: Not enough characters. Failed!" ); }
# &zenlog( "#### Function input: $input" ) if $input ne '["1"]';

# decode JSON
require Zevenet::API31::HTTP;
require JSON;
JSON->import();

# decode input into data reference
my $args;
if ( eval { $args = decode_json( $input ); } )
     { 1 || &zenlog( "#### Function input JSON decode: [OK]" ); } ####
else { &zenlog( "#### Function input JSON decode: Failed!" ); } ####



## Run call

# make code reference from function name
my $code_ref = \&{ $func };
my $returned_value;

# call api request function
eval { $returned_value = [ $code_ref->( @{ $args } ) ] };

&zenlog("$mod->{ name }::$func returned ". $returned_value );
&zenlog("$mod->{ name }::$func returned value dump ". Dumper( $returned_value ) );

if ( $@ )
{
	&zenlog( "#### Function run $func: Failed!" );
	&zenlog( $@ );
	die $@;
}

unless ( defined $returned_value ) { &zenlog( "#### Function output defined: Failed!" ); } ####

my $output = '';

if ( ref $returned_value )
{
	## Proccess output

	# Encode output in JSON and print it
	my $json = JSON->new();

	#~ unless ( eval { $json_out = encode_json( $returned_value ); } )
	unless ( eval { $output = $json->pretty->indent->canonical->allow_blessed->convert_blessed->encode( $returned_value ); } )
	{ &zenlog( "#### Function output JSON encode: Failed!" ); }

	# Optionally send JSON to system log
	#~ &zenlog( "json: '$_'" ) for split ( /\\n/, $output );
}
else
{
	$output = $returned_value;
}

# Send Output to STDOUT
print $output;

# Send memory usage info to system log
if ( &debug() )
{
	my $msg = "MODULE: $mod->{ name } FUNCTION: $func " . &getMemoryUsage();

	&zenlog( $msg );
}

exit 0;



=begin nd
Function: getModuleInfo

	Get some information from a module


	Variable		Example
	--------------------------------------------------------
	'lib_path'    '/usr/share/perl5',
	'path'        'Zevenet/Cluster.pm',
	'fullpath'    '/usr/share/perl5/Zevenet/Cluster.pm',
	'key'         'Zevenet/Cluster.pm',
	'name'        'Zevenet::Cluster',
	'epath'       'Zevenet/Cluster.pme',
	'enc'         'false'


Parameters:
	mod_space - .

Returns:
	undef
=cut
sub getModuleInfo
{
	my $mod = shift;

	# Predefined lib path
	$mod->{ lib_path } = '/usr/share/perl5';

	# There are 2 possible paths:
	# - path	=> is a .pm file
	# - epath	=> is a .pme file
	$mod->{ path } = "$mod->{ name }.pm";
	$mod->{ path } =~ s#\:\:#\/#g;    # Replace :: with /
	$mod->{ epath } = "$mod->{ path }e";

	# Store in $mod->{ key } the correct path relative to $mod->{ lib_path }
	if ( -f "$mod->{ lib_path }/$mod->{ path }" )
	{
		$mod->{ enc } = 'false';
		$mod->{ key } = "$mod->{ path }";
	}
	elsif ( -f "$mod->{ lib_path }/$mod->{ epath }" )
	{
		$mod->{ enc } = 'true';
		$mod->{ key } = "$mod->{ epath }";
	}
	else
	{
		$mod->{ key } = '';
		&zenlog( "FAILURE!!! \$mod->{ lib_path }/\$mod->{ key }: $mod->{ lib_path }/$mod->{ key }" );
	}

	# Store in $mod->{ fullpath } the full path to the module
	$mod->{ fullpath } = "$mod->{ lib_path }/$mod->{ key }";

	# Debugging message
	&zenlog( Data::Dumper->Dump( [$mod], ['mod'] ) ) if $DEBUG;
}

=begin nd
Function: include

	Load Enterprise module, encrypted or not.


	Example %INC:
	{
		'Zevenet/Debug.pme' => '/usr/share/perl5/Zevenet/Debug.pme'
		...
	};


Parameters:
	mod_space - .

Returns:
	undef
=cut
sub include
{
	my $mod;
	$mod->{ name } = shift @_;
	my $func = shift;

	&getModuleInfo( $mod );

	# Finish if the module is already loaded
	return if exists $INC{ $mod->{ key } };

	my $code;

	if ( $mod->{ enc } eq 'false' )    # Load unencrypted module
	{
		require $mod->{ fullpath };
		$code = `cat $mod->{ fullpath }`;
	}
	elsif ( $mod->{ enc } eq 'true' )    # Load encrypted module
	{
		my $uname          = &getGlobalConfiguration( 'uname' );
		my $kernel_release = `$uname -r`;

		my $cmd = "openssl aes-256-cbc -d -a -nosalt -k \"${SYMKEY}\" -in $mod->{ fullpath }";
		$cmd .= " -md md5" if $kernel_release =~ /^4.9/;

		# get code
		$code = `$cmd`;

		&zenlog("[include] Module load BEGIN :: $mod->{ name }");

		# load module
		eval $code;

		&zenlog("[include] Module load END :: $mod->{ name }");

		# log error messages if any happened
		if ( $@ )
		{
			&zenlog( "### Module name: $mod->{ name }" );
			&zenlog( "### Module path: $mod->{ fullpath }" );
			&zenlog( $@ );
			die $@;
		}
		else
		{
			$INC{ $mod->{ key } } = $mod->{ fullpath };
		}
	}

	if ( $func )
	{
		# Function. Check the function is in the module, load params from input in JSON
		my @code = split( "\n", $code );
		&zenlog("enterprise.bin function found: '$_'") for grep( /^sub /, @code );
		&zenlog("enterprise.bin code: '$_'") for grep( /$func/, @code );

		my $func_found = grep { /^sub $func(?:\W|$)/ } split( "\n", $code );
		&zenlog("enterprise.bin count: $func_found");

		# check if the function is available
		unless ( $func_found )
		{
			die "$basename: definition of function $func not found in $mod->{ name }";
		}
	}
}



############################################
### Activation certificate code ############
############################################
#~ use Time::Local;
#~ use Zevenet::Config;
#~ use Zevenet::SystemInfo;

# build local key
sub keycert
{
	my $dmi      = &get_sys_uuid();
	my $hostname = &getHostname();

	my $block1 = crypt ( "${dmi}${hostname}", "93" );
	my $block2 = crypt ( "${hostname}${dmi}", "a3" );
	my $block3 = crypt ( "${dmi}${hostname}", "ZH" );
	my $block4 = crypt ( "${hostname}${dmi}", "h7" );
	$block1 =~ s/^93//;
	$block2 =~ s/^a3//;
	$block3 =~ s/^ZH//;
	$block4 =~ s/^h7//;

	my $str = "${block1}-${block2}-${block3}-${block4}";

	return $str;
}

# evaluate certificate
sub certcontrol
{
	my $basedir = &getGlobalConfiguration( 'basedir' );
	my $zlbcertfile = "$basedir/zlbcertfile.pem";
	my $swcert = 0;

	if ( ! -e $zlbcertfile )
	{
		#swcert = 1 ==> There isn't certificate
		$swcert = 1;
		return $swcert;
	}

	my $openssl_bin = "/usr/bin/openssl";
	my $keyid       = "4B:1B:18:EE:21:4A:B6:F9:76:DE:C3:D8:86:6D:DE:98:DE:44:93:B9";
	my @months      = qw(Jan Feb Mar Apr May Jun Jul Aug Sep Oct Nov Dec);
	my $hostname    = &getHostname();
	my $key         = &keycert();
	my @zen_cert    = `$openssl_bin x509 -in $zlbcertfile -noout -text 2>/dev/null`;

	if (    ( !grep /$key/, @zen_cert )
		 || ( !grep /keyid:$keyid/, @zen_cert )
		 || ( !grep /CN=$hostname\/|CN = $hostname\,/, @zen_cert ) )
	{
		#swcert = 2 ==> Cert isn't signed OK
		$swcert = 2;
		return $swcert;
	}

	# Certificate validity date
	my ( $nb ) = grep /Not Before/i, @zen_cert;
	$nb =~ s/.*not before.*:\ //i;

	my ( $month, $day, $hours, $min, $sec, $year ) = split /[ :]+/, $nb;
	( $month ) = grep { $months[$_] eq $month } 0..$#months;
	my $ini = timegm( $sec, $min, $hours, $day, $month, $year );

	# Certificate expiring date
	my ( $na ) = grep /Not After/i, @zen_cert;
	$na =~ s/.*not after.*:\ //i;

	( $month, $day, $hours, $min, $sec, $year ) = split /[ :]+/, $na;
	( $month ) = grep { $months[$_] eq $month } 0..$#months;
	my $end = timegm( $sec, $min, $hours, $day, $month, $year );

	# Validity remaining
	my $totaldays = ( $end - $ini ) / 86400;
	$totaldays =~ s/\-//g;
	my $dayright = ( $end - time () ) / 86400;

	if ( $dayright < 0 )
	{
		#control errors
		if ( $totaldays < 364 )
		{
			# Policy: expired testing certificates would not stop zen service,
			# but rebooting the service would not start the service,
			# interfaces should always be available.
			$swcert = 3;
		}

		if ( $totaldays > 364 )
		{
			# The contract support plan is expired you have to request a
			# new contract support. Only message alert!
			$swcert = -1;
		}
	}

	# error codes
	#swcert = 0 ==> OK
	#swcert = 1 ==> There isn't certificate
	#swcert = 2 ==> Cert isn't signed OK
	#swcert = 3 ==> Cert test and it's expired
	#swcert = -1 ==> Cert support and it's expired

	#output
	return $swcert;
}

sub get_sys_uuid
{
	my ( $dmi ) = grep ( /UUID\:/, `/usr/sbin/dmidecode` );
	( undef, $dmi ) = split ( /:\s+/, $dmi );

	chomp $dmi;

	return $dmi;
}

sub checkActivationCertificate
{
	my $swcert = &certcontrol();

	# if $swcert is greater than 0 zapi should not work
	if ( $swcert > 0 )
	{
		my $msg;

		if ( $swcert == 1 )
		{
			$msg =
			  "There isn't a valid Zen Load Balancer certificate file, please request a new one";
		}
		elsif ( $swcert == 2 )
		{
			$msg =
			  "The certificate file isn't signed by the Zevenet Certificate Authority, please request a new one";
		}
		elsif ( $swcert == 3 )
		{
			# Policy: expired testing certificates would not stop zen service,
			# but rebooting the service would not start the service,
			# interfaces should always be available.
			$msg =
			  "The Zen Load Balancer certificate file you are using is for testing purposes and its expired, please request a new one";
		}

		my $body = {
					 message         => $msg,
					 certificate_key => &keycert(),
					 hostname        => &getHostname(),
		};

		return &httpResponse( { code => 403, body => $body } );
	}

	return $swcert;
}
